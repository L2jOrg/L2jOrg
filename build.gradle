plugins {
    id "idea"
    id "eclipse"
    id "distribution"
    id "net.nemerosa.versioning" version "2.8.2"
    id "org.sonarqube" version "2.8"
}

version '1.7.0'
ext.revision = versioning.info.build
ext.buildJDK = "${System.properties['java.version']} (${System.properties['java.vendor']} ${System.properties['java.vm.version']})".toString()

defaultTasks('clean', 'build')

subprojects {
    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'eclipse'

    compileJava.options.encoding = 'UTF-8'

    repositories {
        mavenCentral()

        maven {
            url 'https://oss.sonatype.org/content/repositories/snapshots/'
        }
    }
    
    sourceCompatibility = JavaVersion.VERSION_14
    targetCompatibility = JavaVersion.VERSION_14

    afterEvaluate {
        compileJava {
            inputs.property("moduleName", moduleName)
            doFirst {
                options.compilerArgs = [
                    '--module-path', classpath.asPath,
                    '-parameters', '--enable-preview'
                ]
                options.compilerArgs << "-Xlint:unchecked"
                classpath = files()
            }
        }
    }
}

distZip {
    subprojects.each { dependsOn("${it.name}:build") }
}

distTar {
    subprojects.each { dependsOn("${it.name}:build") }
}

distributions {
    subprojects.each { distTar.enabled = false}
    main {
        contents {
            from(["${project(':AuthServer').buildDir}/authserver"])  {
                into("authserver")
            }

            from(["Datapack", "${project(':Gameserver').buildDir}/gameserver"]) {
                into("gameserver")
            }

            from("database/installer") {
                into("gameserver/sql")
            }

            from("database/installer") {
                into("authserver/sql")
            }
        }
    }
}

sonarqube {
    properties {
        property "sonar.projectKey", "JoeAlisson_L2jOrg"
        property "sonar.organization", "joealisson-github"
        property "sonar.host.url", "https://sonarcloud.io"
        property "sonar.login", "0d1d283bbefe1d176b865d0f122881ef869fa2f2"
    }
}