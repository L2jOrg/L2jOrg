plugins {
    id "idea"
    id "eclipse"
    id "distribution"
    id "net.nemerosa.versioning" version "2.15.0"
    id "org.sonarqube" version "3.3"
}

version '1.7.0-RC1'
ext.revision = versioning.info.build
ext.buildJDK = "${System.getProperty('java.version')} (${System.getProperty('java.vendor')} ${System.getProperty('java.vm.version')})".toString()

defaultTasks('clean', 'build')

subprojects {
    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'eclipse'

    compileJava.options.encoding = 'UTF-8'

    repositories {
        mavenCentral()

        maven {
            url 'https://oss.sonatype.org/content/repositories/snapshots/'
        }
    }

    sourceCompatibility = JavaVersion.VERSION_20
    targetCompatibility = JavaVersion.VERSION_20

    afterEvaluate {
        compileJava {
            inputs.property("moduleName", moduleName)
            doFirst {
                options.compilerArgs = [
                    '--module-path', classpath.asPath,
                    '-parameters', '--enable-preview'
                ]
                options.compilerArgs << "-Xlint:unchecked"
                classpath = files()
            }
        }
    }
}

distZip {
    subprojects.each {
        dependsOn("${it.name}:build")
    }
    dependsOn("${project(':AuthServer').name}:dist", "${project(':Gameserver').name}:dist")
}

distributions {
    subprojects.each { distTar.enabled = false}
    main {
        contents {
            from(["${project(':AuthServer').buildDir}/authserver"])  {
                into("authserver")
            }

            from(["Datapack", "${project(':Gameserver').buildDir}/gameserver"]) {
                into("gameserver")
            }

            from("database/installer") {
                into("gameserver/sql")
            }

            from("database/installer") {
                into("authserver/sql")
            }
        }
    }
}

sonarqube {
    properties {
        property "sonar.projectKey", "L2jOrg_L2jOrg"
        property "sonar.organization", "l2jorg"
        property "sonar.host.url", "https://sonarcloud.io"
    }
}
